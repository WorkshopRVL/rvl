############################################################
# Property Mapping - Concerning Order:
############################################################
spin:constraint ...#########################################
############################################################

# The source values are ordered. Shouldn't be the target side also be ordered? You may ignore the information on order.
ASK WHERE {
    ?this <http://purl.org/rvl/rvl2avm/spin/helper_sources_ordered> true .
    NOT EXISTS {
        ?this <http://purl.org/rvl/rvl2avm/spin/helper_targets_ordered> true .
    } .
}

# The target values are ordered. Shouldn't be the source side also be ordered? You may ignore this.
ASK WHERE {
    ?this <http://purl.org/rvl/rvl2avm/spin/helper_targets_ordered> true .
    NOT EXISTS {
        ?this <http://purl.org/rvl/rvl2avm/spin/helper_sources_ordered> true .
    } .
}

# Property must be at least transitive to be usable as order relation property
ASK WHERE {
    ?this rvl:orderSourceValuesBy ?orp .
    NOT EXISTS {
        ?orp a owl:TransitiveProperty .
    } .
}



# source value bounds may only be set when values are ordered
ASK WHERE {
    {
        ?this rvl:sourceValueUpperBoundDTP ?b .
    }
    UNION
    {
        ?this rvl:sourceValueLowerBoundDTP ?b .
    }
    UNION
    {
        ?this rvl:sourceValueUpperBoundIncl ?b .
    }
    UNION
    {
        ?this rvl:sourceValueLowerBoundIncl ?b .
    } .
    NOT EXISTS {
        ?this <http://purl.org/rvl/rvl2avm/spin/helper_sources_ordered> true .
    } .
}


# target value bounds (min/max) may only be set when values are ordered (any bounds - OP and DTP bounds)
ASK WHERE {
    {
        ?this rvl:targetValueUpperBoundIncl ?b .
    }
    UNION
    {
        ?this rvl:targetValueLowerBoundIncl ?b .
    } .
    NOT EXISTS {
        ?this <http://purl.org/rvl/rvl2avm/spin/helper_targets_ordered> true .
    } .
}

############################################################
spin:rule ... ##############################################
############################################################


# sources are NOT ordered for string and boolean DTP properties
CONSTRUCT {
    ?this <http://purl.org/rvl/rvl2avm/spin/helper_sources_ordered> false .
}
WHERE {
    ?this rvl:sourceProperty ?sp .
    ?sp a owl:DatatypeProperty .
    {
        ?sp rdfs:range xsd:boolean .
    }
    UNION
    {
        ?sp rdfs:range xsd:string .
    } .
}


# sources are ordered when a list or order relation is given for OP, or for numeral DTP properties
CONSTRUCT {
    ?this <http://purl.org/rvl/rvl2avm/spin/helper_sources_ordered> true .
}
WHERE {
    ?this rvl:sourceProperty ?sp .
    ?sp a rdf:Property .
    {
        {
            ?this rvl:orderSourceValuesBy ?orel .
            ?orel a owl:TransitiveProperty .
        }
        UNION
        {
            ?this rvl:sourceValueSet ?sval .
            ?sval a rdf:List .
        }
        UNION
        {
            ?this rvl:sourceProperty ?sp .
            ?sp a owl:DatatypeProperty .
            {
                {
                    ?sp rdfs:range xsd:float .
                }
                UNION
                {
                    ?sp rdfs:range xsd:int .
                } .
            } .
        } .
    } .
}


# targets are ordered when a value LIST is given, or when a continuous attribute is chosen
CONSTRUCT {
    ?this <http://purl.org/rvl/rvl2avm/spin/helper_targets_ordered> true .
}
WHERE {
    {
        ?this rvl:targetValueList ?tvallist .
    }
    UNION
    {
        ?this rvl:targetGraphicRelation ?tvm .
        ?tvm a viso-graphic:GraphicAttributeContinuous .
    } .
}


# targets are ordered when all values have an ordinal value attached to them
# HERE also spin-owl could be used in combination with an all-quantification restriction on the class
CONSTRUCT {
    ?this <http://purl.org/rvl/rvl2avm/spin/helper_targets_ordered> true .
}
WHERE {
    ?this rvl:targetValue ?tv1 .
    ?tv1 viso-data:has_ordinal_value ?ov1 .
    NOT EXISTS {
        ?this rvl:targetValue ?tv .
        NOT EXISTS {
            ?tv viso-data:has_ordinal_value ?ov .
        } .
    } .
}


############################################################
############################################################
############################################################
############################################################

# Difficult now, since Property Mapping, Value Mapping and Interval involved. 
# extra DTPBounds really Necessary?

# DTP bounds may not be set when we are mapping a discrete (ObjectProperty) source property
ASK WHERE {
    ?this rvl:sourceProperty ?sp .
    {
        ?this rvl:sourceValueUpperBoundDTP ?ub .
    }
    UNION
    {
        ?this rvl:sourceValueLowerBoundDTP ?lb .
    } .
    ?sp a owl:ObjectProperty .
}

# OP bounds may not be set when we are mapping a continuous (DT) source property
ASK WHERE {
    ?this rvl:sourceProperty ?sp .
    {
        ?this rvl:sourceValueUpperBoundIncl ?ub .
    }
    UNION
    {
        ?this rvl:sourceValueLowerBoundIncl ?lb .
    } .
    ?sp a owl:DatatypeProperty .
}

# when SourceValueOutOfBounds is attached to PM as a general setting, this makes no sense

# SourceValueOutOfBounds may only be defined, when some bound is defined.
ASK WHERE {
    ?this rvl:sourceValuesOutOfBounds ?oob .
    NOT EXISTS {
        ?this rvl:sourceValueUpperBoundIncl ?up .
    } .
    NOT EXISTS {
        ?this rvl:sourceValueLowerBoundIncl ?lb .
    } .
    NOT EXISTS {
        ?this rvl:sourceValueUpperBoundDTP ?dtpup .
    } .
    NOT EXISTS {
        ?this rvl:sourceValueLowerBoundDTP ?dtplb .
    } .
}

# also more difficult now:

# Value lists may only be used as cyclic lists, when there are more source than target values defined. !!! MAKES NO SENSE AT THE MOMENT, SINCE IT ONLY WORKS ON VALUELISTS, but checks if single values are given! DIFFICULT to count LIST members?!
ASK WHERE {
    ?this rvl:targetValueCycle ?useValueListAsCycle .
    ?this rvl:sourceValue ?sv .
    ?this rvl:targetValue ?tv .
    FILTER (spl:objectCount(?this, rvl:sourceValue) <= spl:objectCount(?this, rvl:targetValue)) .
}

# more complex now, since VM can define ranges:

# Value mappings may only be defined for property mapping that do NOT map to continuous attributes
ASK WHERE {
    ?this rvl:valueMapping ?vm .
    ?this rvl:targetGraphicRelation ?tvm .
    ?tvm a viso-graphic:GraphicAttributeContinuous .
}

############################################################
############################################################
############################################################
############################################################
